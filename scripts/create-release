#! /usr/bin/env zsh

set -eu -o pipefail -o null_glob

# Check if exactly one argument was passed: the new version.
if (( $# != 1 )) || [[ "$1" == "--help" ]]; then
  >&2 {
    echo "Usage: $0 <version>"
    echo "  version: The new version, e.g., v1.0.0 or v1.0.0"
  }
  exit 1
fi

# Verify the tag, and extract the title from the tag.
tag=${1}
if ! [[ "${tag}" =~ '^v([0-9]+\.){2}([0-9]+)$' ]]; then
  >&2 echo "Error: ${tag} is not a valid version number"
  exit 1
fi
title="Tenzir App ${tag%%.0}"

# Check if all required tools are available.
for binary in 'git' 'gh'; do
  if ! command -v "${binary}" 2>&1 >/dev/null; then
    >&2 echo "Error: ${binary} not in PATH"
    exit 1
  fi
done

# To simplify this script, we require a clean working directoy.
if ! [ -z "$(git status --porcelain)" ]; then
  >&2 echo "Error: working directory unclean"
  exit 1
fi

# We must be on the main branch to run this script.
if ! [ "$(git rev-parse --abbrev-ref HEAD)" = "main" ]; then
  >&2 echo "Error: not on the main branch"
  exit 1
fi

# Ensure that we are on the latest version of the branch.
git fetch origin main
git reset --hard origin/main

# Create the tag and push it.
git tag -s "${tag}" -m "${title}"
git push --follow-tags origin

# Prompt to cut the release.
gh release create "${tag}" --title "${title}" --generate-notes
